func () > main > () {
    List list = make List < ();
    
    list.append(make Node < (10));
    list.append(make Node < (4));
    list.append(make Node < (3));

    list.print();
}

tmpl List {
    Node head;
    int length;

    constructor < () {
        this.length = 0;
    }

    func (Node node) > append > () {
        if (this.length == 0) {
            this.head = node;
        } else {
            Node last = this.get(this.length - 1);
            last.next = node;
        }

        this.length = this.length + 1;
    }

    func (int depth) > get > (Node) {
        Node node = this.head;
        for (; depth != 0; depth = depth - 1) {
            node = node.next;
        }

        return node;
    }

    func () > print > () {
        Node node = this.head;
        for (int i = 0; i != this.length; i = i + 1) {
            printf("Index: %d Value: %d \n", i, node.value);
            node = node.next;
        }
    }
}

tmpl Node {
    Node next;
    int value;

    constructor < (int val) {
        this.value = val;
    }
}
